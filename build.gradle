plugins {
    id "java"
    id "application"
    id "org.openjfx.javafxplugin" version "$javafxpluginVersion"
    id "org.beryx.jlink" version "$jlinkVersion"
}

repositories {
    mavenCentral()
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

javafx {
    version = "$javafxVersion"
    modules = ["javafx.controls", "javafx.fxml", "javafx.swing"]
}

application {
    getMainClass().set("random.Main")
}

dependencies {
    implementation "org.knowm.xchart:xchart:$xchartVersion"
}

jlink {
    launcher {
        name = "stego"
    }

    // Install Wix Toolset before using jpackage. And add it to the PATH variable
    // https://wixtoolset.org/docs/wix3/
    jpackage {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        if (currentOs.windows) {
            installerOptions += ["--win-per-user-install", "--win-dir-chooser", "--win-menu"]
        }
    }
}

tasks.register("fatJar", Jar) {
    group "build"
    manifest { attributes "Main-Class": "random.Main" }
    archiveFileName = "stego.jar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
